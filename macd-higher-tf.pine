//@version=5
indicator(title="MACD Higher TimeFrame", shorttitle="MACD Higher TF")

// Input MACD Length
fast_length     = input(title="Fast Length", defval=12, group = 'MACD')
slow_length     = input(title="Slow Length", defval=26, group = 'MACD')
src             = input(title="Source", defval=close, group = 'MACD')
signal_length   = input.int(title="Signal Smoothing",  minval = 1, maxval = 50, defval = 9, group = 'MACD')
sma_source      = input.string(title="Oscillator MA Type",  defval="EMA", options=["SMA", "EMA"], group = 'MACD')
sma_signal      = input.string(title="Signal Line MA Type", defval="EMA", options=["SMA", "EMA"], group = 'MACD')
higher_tf = input.timeframe(defval = '1W', title = 'Higher Timeframe')

// MACD color
col_macd       = input(#2962FF, title = "MACD Line",    group="MACD Color Settings", inline="MACD")
col_signal     = input(#FF6D00, title = "Signal Line",  group="MACD Color Settings", inline="Signal")
col_grow_above = input(#26A69A, title = "Above Grow",   group="MACD Histogram",      inline="Above")
col_fall_above = input(#B2DFDB, title = "Fall",         group="MACD Histogram",      inline="Above")
col_grow_below = input(#FFCDD2, title = "BelowGrow",    group="MACD Histogram",      inline="Below")
col_fall_below = input(#FF5252, title = "Fall",         group="MACD Histogram",      inline="Below")

// Initial MACD
fast_ma = sma_source == "SMA" ? ta.sma(src, fast_length) : ta.ema(src, fast_length)
slow_ma = sma_source == "SMA" ? ta.sma(src, slow_length) : ta.ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? ta.sma(macd, signal_length) : ta.ema(macd, signal_length)

higher_tf_macd = request.security(symbol = syminfo.tickerid, timeframe = higher_tf, expression = macd[1], gaps = barmerge.gaps_on, lookahead = barmerge.lookahead_on)
higher_tf_signal = request.security(symbol = syminfo.tickerid, timeframe = higher_tf, expression = signal[1], gaps = barmerge.gaps_on, lookahead = barmerge.lookahead_on)
higher_tf_hist = higher_tf_macd - higher_tf_signal

// array
var float temp_hist_weekly = na // representative of 'higher_tf_hist'
var float temp_hist_day = na // temp variable to handle 'na'

if na(higher_tf_hist)
    temp_hist_weekly := temp_hist_day
else
    temp_hist_day := higher_tf_hist
    temp_hist_weekly := higher_tf_hist


// MACD plot
hline(0, "Zero Line", color=color.new(#787B86, 50))
plot(higher_tf_hist, title="Histogram", style=plot.style_columns, color=(temp_hist_weekly>=0 ? (temp_hist_weekly[1] < temp_hist_weekly ? col_grow_above : col_fall_above) : (temp_hist_weekly[1] < temp_hist_weekly ? col_grow_below : col_fall_below)))
plot(higher_tf_macd, title="MACD", color=col_macd)
plot(higher_tf_signal, title="Signal", color=col_signal)

